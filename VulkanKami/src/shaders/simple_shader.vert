#version 450
#extension GL_KHR_vulkan_glsl : enable

layout(location = 0) in vec3 position;
layout(location = 1) in vec3 color;
layout(location = 2) in vec3 normal;
layout(location = 3) in vec2 uv;

layout(location = 0) out vec3 fragColor;
layout(location = 1) out vec3 fragPosWorld; // Need this to calculate direction to light source for EACH fragment (linearly interpolated)
layout(location = 2) out vec3 fragNormalWorld; // Also interpolated for the fragment shader
layout(location = 3) out vec2 fragUV;

struct PointLight {
    vec4 position; // ignore w
    vec4 color; // w is intensity
};

layout(set = 0, binding = 0) uniform GlobalUbo { // Set and Binding numbers must match what we set when setting up the descriptorSetLayout
    mat4 projection; // Could send projectView precomputed for a slight performance increase
    mat4 view;
    mat4 invView;
    vec4 ambientLightColor; // W is intensity
    PointLight pointLights[100]; // Can use Specialization Constants instead of this
    int numLights;
} ubo;

layout(push_constant) uniform Push { // Ideally we would want to push projection,view, and model matrices but for now we can only do 2 4x4
    mat4 modelMatrix;
    mat4 normalMatrix;
} push;

// Note whenever doing calculations in a shader ensure that ALL VALUES are within the same space such as world space/object space
void main() {
vec4 positionWorld =  push.modelMatrix * vec4(position, 1.0); 
    gl_Position = ubo.projection * ubo.view * positionWorld; // () to prevent expensive matrix * matrix twice

    // Need to convert the normals into world space since the light direction is in world space, this is done by * with modelMatrix
    // Converted to mat3 since we do not need the translation data in mat4 since normals represent directions and not positions and are not affected
    // by translations
    // temporary: this is only correct in certain situations!
    // vec3 normalWorldSpace = normalize(mat3(push.modelMatrix) * normal);

    // calculating the inverse in a shader can be expensive and should be avoided
    // mat3 normalMatrix = transpose(inverse(mat3(push.modelMatrix)));
    // vec3 normalWorldSpace = normalize(normalMatrix * normal);

    fragNormalWorld = normalize(mat3(push.normalMatrix) * normal);
    fragPosWorld = positionWorld.xyz;
    fragColor = color;
    fragUV = uv;
}